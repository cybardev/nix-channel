{
  lib,
  python3,
  fetchFromGitHub,
  rustPlatform,
}: let
  pname = "pyrefly";
  version = "0.17.1";
  hashes = {
    pyrefly = "sha256-aMFK4verIdijgunVmj+Ge5XZZir38PLAKWNw4mOieiM=";
    ruff_python_ast = "sha256-YQmkmn6fWKmKz+Lw9wwgPG2O9E+2/ZJcclRs97SOWK4=";
  };
  src = fetchFromGitHub {
    owner = "facebook";
    repo = "pyrefly";
    rev = version;
    hash = hashes.pyrefly;
  };
in
  python3.pkgs.buildPythonApplication {
    inherit pname version;
    format = "pyproject";

    disabled = python3.pythonOlder "3.8";

    inherit src;

    postPatch = ''
      cp ${./Cargo.lock} pyrefly/Cargo.lock
    '';

    buildAndTestSubdir = "pyrefly";
    cargoRoot = "pyrefly";

    cargoDeps = rustPlatform.importCargoLock {
      # generated by running `cargo generate-lockfile` in the `pyrefly` directory
      lockFile = ./Cargo.lock;
      outputHashes = {
        "ruff_python_ast-0.0.0" = hashes.ruff_python_ast;
      };
    };

    nativeBuildInputs = with rustPlatform; [
      maturinBuildHook
    ];

    # disable tests
    doCheck = false;

    meta = with lib; {
      description = "A fast type checker and IDE for Python";
      homepage = "https://github.com/facebook/pyrefly";
      license = licenses.mit;
      mainProgram = "pyrefly";
      maintainers = [];
      platforms = lib.platforms.linux ++ lib.platforms.darwin;
    };
  }
