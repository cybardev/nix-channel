{
  lib,
  fetchFromGitHub,
  rustPlatform,
}: let
  pname = "pyrefly";
  version = "0.17.1";
in
  rustPlatform.buildRustPackage {
    inherit pname version;

    src = fetchFromGitHub {
      owner = "facebook";
      repo = "pyrefly";
      rev = version;
      hash = "sha256-aMFK4verIdijgunVmj+Ge5XZZir38PLAKWNw4mOieiM=";
    };

    postPatch = ''
      cp ${./Cargo.lock} Cargo.lock
      cp ${./Cargo.lock} pyrefly/Cargo.lock
    '';

    buildAndTestSubdir = "pyrefly";
    cargoRoot = "pyrefly";

    cargoDeps = rustPlatform.importCargoLock {
      # generated by running `cargo generate-lockfile` in the `pyrefly` directory
      lockFile = ./Cargo.lock;
      outputHashes = {
        "ruff_python_ast-0.0.0" = "sha256-YQmkmn6fWKmKz+Lw9wwgPG2O9E+2/ZJcclRs97SOWK4=";
      };
    };

    # requires unstable rust features
    RUSTC_BOOTSTRAP = 1;

    nativeBuildInputs = [rustPlatform.cargoSetupHook];

    meta = with lib; {
      description = "A fast type checker and IDE for Python";
      homepage = "https://github.com/facebook/pyrefly";
      license = licenses.mit;
      mainProgram = pname;
      platforms = lib.platforms.linux ++ lib.platforms.darwin;
      # maintainers = with maintainers; [cybardev];
    };
  }
